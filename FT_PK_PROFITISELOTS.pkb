CREATE OR REPLACE PACKAGE BODY FT_PK_PROFITISELOTS AS

  cVersionControlNo   VARCHAR2(12) := '1.0.1'; -- Current Version Number

  FUNCTION CURRENTVERSION(IN_BODYORSPEC IN INTEGER ) RETURN VARCHAR2
  IS
  BEGIN
    IF  IN_BODYORSPEC = CONST.C_SPEC THEN
      RETURN cSpecVersionControlNo;
    ELSE  
      RETURN cVersionControlNo;
    END IF;                
  END CURRENTVERSION;

  PROCEDURE LOTPROFITABILITY(LITITENO_IN LOTITE.LITITENO%TYPE)
  IS
    PARAMETER_LIST      FT_PK_STRING_UTILS.TYPE_STRING_TOKENS;
  BEGIN
    IF LITITENO_IN IS NULL THEN
      PARAMETER_LIST('#PARAMNAME') := 'LITITENO_IN';
      PARAMETER_LIST('#PARAMVALUE') := TO_CHAR(LITITENO_IN);
      FT_PK_ERRORS.RAISE_ERROR(FT_PK_ERRNUMS.FT_PARAMETER, PARAMETER_LIST);
    END IF;
  
    UPDATE LOTPROFITSALOFF
    SET GROSSSALES = 0.0,
        NETTSALES = 0.0,
        PROFIT = 0.0,
        PROFITPERC = 0.0,
        PREVPROFIT = PROFIT,
        SOLDQTYEQUIV = 0,
        LHEPROFITPERC = 0.0,
        POCOSTS = NVL((	SELECT SUM(ITECHG.ICHAPPAMT)
                         FROM ITECHG ITECHG, EXPCHA EXPCHA
                         WHERE ITECHG.EXCRECNO = EXPCHA.EXCCHAREC
                           AND NVL(EXPCHA.EXCRECOVFROMPL, CONST.C_FALSE) = CONST.C_FALSE
                           AND ITECHG.ICHISTRECNO IS NULL
                           AND ITECHG.CTYNO <> CONST.CTYGOODS
                           AND LOTPROFITSALOFF.LITITENO = ITECHG.LITRECNO), 0.0),
        GOODSCOST = NVL(( SELECT SUM(ITECHG.ICHAPPAMT)
                           FROM ITECHG ITECHG, EXPCHA EXPCHA
                           WHERE ITECHG.EXCRECNO = EXPCHA.EXCCHAREC
                             AND NVL(EXPCHA.EXCRECOVFROMPL, CONST.C_FALSE) = CONST.C_FALSE
                             AND ITECHG.ICHISTRECNO IS NULL
                             AND ITECHG.CTYNO = CONST.CTYGOODS
                             AND LOTPROFITSALOFF.LITITENO = ITECHG.LITRECNO), 0.0)
    WHERE LOTPROFITSALOFF.LITITENO = LITITENO_IN;

    MERGE INTO LOTPROFITSALOFF LOTPROFITSALOFF
    USING (
      SELECT  SALES_VIEW.LITITENO,
              ITESTO.ISTRECNO,
              ITESTO.ISTLOTNO AS LHERECNO,
              PURORD.PORSALOFF,
              NVL(SALES_VIEW.SALES_VALUE, 0.0) AS GROSSSALES,
              NVL(SALES_VIEW.SALES_VALUE, 0.0) - NVL(SALES_COST, 0.0) AS NETTSALES,
              NVL(( SELECT SUM(ITECHG.ICHAPPAMT)
                   FROM ITECHG ITECHG, EXPCHA EXPCHA
                   WHERE ITECHG.LITRECNO = SALES_VIEW.LITITENO
                     AND ITECHG.EXCRECNO = EXPCHA.EXCCHAREC
                     AND NVL(EXPCHA.EXCRECOVFROMPL, CONST.C_FALSE) = CONST.C_FALSE
                     AND ITECHG.ICHISTRECNO IS NULL), 0.0) AS POCOSTS,
              NVL(( SELECT SUM(ITECHG.ICHAPPAMT)
                    FROM ITECHG ITECHG, EXPCHA EXPCHA
                    WHERE ITECHG.LITRECNO = SALES_VIEW.LITITENO
                      AND ITECHG.EXCRECNO = EXPCHA.EXCCHAREC
                      AND NVL(EXPCHA.EXCRECOVFROMPL, CONST.C_FALSE) = CONST.C_FALSE
                      AND ITECHG.CTYNO = CONST.CTYGOODS
                      AND ITECHG.ICHISTRECNO IS NULL), 0.0) AS GOODSCOST,
              NVL(SALES_VIEW.BULK_SALES_QTY, 0.0) AS SOLDQTYEQUIV
    FROM FT_V_PROFITPERLOT SALES_VIEW
    INNER JOIN ITESTO
      ON ITESTO.ISTLITNO = SALES_VIEW.LITITENO AND ITESTO.ISTFSTREC = CONST.C_YES
    INNER JOIN PURORD
      ON PURORD.PORNO = ITESTO.ISTPONO
    WHERE SALES_VIEW.LITITENO = LITITENO_IN
    ) SALESTAB
    ON (SALESTAB.ISTRECNO = LOTPROFITSALOFF.ISTRECNO)
    WHEN MATCHED THEN
    UPDATE
    SET LOTPROFITSALOFF.LHERECNO = SALESTAB.LHERECNO,
        LOTPROFITSALOFF.GROSSSALES = SALESTAB.GROSSSALES,
        LOTPROFITSALOFF.NETTSALES = SALESTAB.NETTSALES,
        LOTPROFITSALOFF.POCOSTS = SALESTAB.POCOSTS - SALESTAB.GOODSCOST,
        LOTPROFITSALOFF.GOODSCOST = SALESTAB.GOODSCOST,
        LOTPROFITSALOFF.PROFIT = SALESTAB.NETTSALES - SALESTAB.POCOSTS,
        LOTPROFITSALOFF.PROFITPERC = CASE WHEN ABS(SALESTAB.NETTSALES) < 0.01 THEN 0.0 ELSE ROUND((SALESTAB.NETTSALES - SALESTAB.POCOSTS) / ABS(SALESTAB.NETTSALES) * 100.0, 2) END,
        LOTPROFITSALOFF.SOLDQTYEQUIV = SALESTAB.SOLDQTYEQUIV
    WHEN NOT MATCHED THEN
    INSERT( LOTPROFITSALOFF.LPSRECNO,
            LOTPROFITSALOFF.LITITENO,
            LOTPROFITSALOFF.ISTRECNO,
            LOTPROFITSALOFF.LHERECNO,
            LOTPROFITSALOFF.SALOFFNO,
            LOTPROFITSALOFF.GROSSSALES,
            LOTPROFITSALOFF.NETTSALES,
            LOTPROFITSALOFF.POCOSTS,
            LOTPROFITSALOFF.GOODSCOST,
            LOTPROFITSALOFF.PROFIT,
            LOTPROFITSALOFF.PROFITPERC,
            LOTPROFITSALOFF.SOLDQTYEQUIV,
            LOTPROFITSALOFF.PROFITISED)
      VALUES  (SP_WIZGETCONTROL('NXTLPSRECNO' , 1, 'FT_PK_PROFITISELOTS'),
              SALESTAB.LITITENO,
              SALESTAB.ISTRECNO,
              SALESTAB.LHERECNO,
              SALESTAB.PORSALOFF,
              SALESTAB.GROSSSALES,
              SALESTAB.NETTSALES,
              SALESTAB.POCOSTS - SALESTAB.GOODSCOST,
              SALESTAB.GOODSCOST,
              SALESTAB.NETTSALES - SALESTAB.POCOSTS,
              CASE WHEN ABS(SALESTAB.NETTSALES) < 0.01 THEN 0.0 ELSE ROUND((SALESTAB.NETTSALES - SALESTAB.POCOSTS) / ABS(SALESTAB.NETTSALES) * 100.0, 2) END,
              SALESTAB.SOLDQTYEQUIV,
              0);
              
    UPDATE LOTPROFITSALOFF
    SET FULLYSOLDINVOICED = NVL((SELECT MIN(CASE WHEN LOTITERCVD.RCVDQTY = LOTITESOLD.SOLDQTY THEN CONST.C_TRUE ELSE CONST.C_FALSE END)
                                 FROM  (SELECT ITESTO.ISTLITNO AS LITITENO, SUM(ITESTO.ISTORGQTY) AS RCVDQTY FROM ITESTO GROUP BY ITESTO.ISTLITNO) LOTITERCVD,
                                       (SELECT SALES_VIEW.LITITENO, SUM(SALES_VIEW.DTLBULKSALESQTY) SOLDQTY FROM FT_V_DPRSTOLOTS SALES_VIEW WHERE DELSTATUS = 'Inv' GROUP BY SALES_VIEW.LITITENO) LOTITESOLD
                                 WHERE LOTITERCVD.LITITENO = LOTITESOLD.LITITENO(+)
                                   AND LOTITERCVD.LITITENO = LOTPROFITSALOFF.LITITENO), CONST.C_FALSE)
    WHERE LOTPROFITSALOFF.LITITENO = LITITENO_IN;
    

    UPDATE LOTPROFITSALOFF
    SET HASGOODSCOST = NVL(( SELECT CASE WHEN ABS(SUM(ITECHG.ICHAPPAMT)) > 0.009 THEN CONST.C_TRUE ELSE CONST.C_FALSE END
    							            FROM ITECHG ITECHG
                             WHERE ITECHG.LITRECNO = LOTPROFITSALOFF.LITITENO
                               AND ITECHG.CTYNO = CONST.CTYGOODS
                               AND ITECHG.ICHISTRECNO IS NULL), CONST.C_FALSE)
    WHERE LOTPROFITSALOFF.LITITENO = LITITENO_IN;


    UPDATE LOTPROFITSALOFF
    SET POCOSTAUTH =	NVL(( SELECT MIN(CASE
                                        WHEN SALOFFNO.LOTPROFITMETH IN(1, 2) AND ABS(NVL(ITECHG.ICHAPPAMT, 0.0) - NVL(AUTHTOGL.AUTHTOGLAMT, NVL(RECOVTOGL.AUTHTOGLAMT, 0.0))) > 0.009 THEN CONST.C_FALSE
                                        WHEN SALOFFNO.LOTPROFITMETH IN(3, 4) AND ABS(NVL(AUTHTOGL.AUTHTOGLAMT, NVL(RECOVTOGL.AUTHTOGLAMT, 0.0))) < 0.01 THEN CONST.C_FALSE
                                        ELSE CONST.C_TRUE
                                      END)
                            FROM ITECHG ITECHG, EXPCHA EXPCHA, SALOFFNO SALOFFNO,
                                ( SELECT ACCITE.AITITERECNO, SUM(CASE WHEN ACCITE.AITDRCR = 'C' THEN -1 * ACCITE.AITBASEAMOUNT ELSE ACCITE.AITBASEAMOUNT END) AS AUTHTOGLAMT
                                  FROM ACCITE ACCITE
                                  WHERE ACCITE.AITGLTRECNO > 0
                                  GROUP BY ACCITE.AITITERECNO) AUTHTOGL,
                                ( SELECT PORECOVITE.PORECOVAITITERECNO, SUM(CASE WHEN PORECOVITE.PORECOVAITDRCR = 'C' THEN -1 * PORECOVITE.PORECOVAITBASEAMT ELSE PORECOVITE.PORECOVAITBASEAMT END) AS AUTHTOGLAMT
                                  FROM PORECOVITE PORECOVITE
                                  WHERE PORECOVITE.PORECOVPSTRECNO = 34
                                    AND PORECOVITE.PORECOVGLTRECNO IS NOT NULL
                                  GROUP BY PORECOVITE.PORECOVAITITERECNO) RECOVTOGL
                            WHERE EXPCHA.EXCCHAREC = ITECHG.EXCRECNO
                              AND SALOFFNO.SALOFFNO = LOTPROFITSALOFF.SALOFFNO
                              AND ITECHG.LITRECNO = LOTPROFITSALOFF.LITITENO
                              AND ITECHG.ICHRECNO = AUTHTOGL.AITITERECNO(+)
                              AND ITECHG.ICHRECNO = RECOVTOGL.PORECOVAITITERECNO(+)
                              AND NVL(EXPCHA.EXCRECOVFROMPL, CONST.C_FALSE) = CONST.C_FALSE
                              AND ITECHG.CTYNO = CASE WHEN SALOFFNO.LOTPROFITMETH IN(2, 4) THEN CONST.CTYGOODS ELSE ITECHG.CTYNO END
                              AND ITECHG.ICHISTRECNO IS NULL
                            GROUP BY SALOFFNO.LOTPROFITMETH), CONST.C_TRUE)
    WHERE LOTPROFITSALOFF.LITITENO = LITITENO_IN;
    
    UPDATE LOTPROFITSALOFF
    SET SALESCOSTAUTH = NVL(( SELECT MIN(CASE
                                            WHEN SALOFFNO.LOTPROFITMETH IN(1, 2) AND ABS(NVL(ITECHG.ICHAPPAMT, 0.0) - NVL(AUTHTOGL.AUTHTOGLAMT, NVL(RECOVTOGL.AUTHTOGLAMT, 0.0))) > 0.009 THEN CONST.C_FALSE
                                            WHEN SALOFFNO.LOTPROFITMETH IN(3, 4) AND ABS(NVL(AUTHTOGL.AUTHTOGLAMT, NVL(RECOVTOGL.AUTHTOGLAMT, 0.0))) < 0.01 THEN CONST.C_FALSE
                                            ELSE CONST.C_TRUE
                                         END)
                              FROM ITECHG, EXPCHA, SALOFFNO, FT_V_DPRSTOLOTS SALES_VIEW,
                                  ( SELECT ACCITE.AITITERECNO, SUM(CASE WHEN ACCITE.AITDRCR = 'C' THEN -1 * ACCITE.AITBASEAMOUNT ELSE ACCITE.AITBASEAMOUNT END) AS AUTHTOGLAMT
                                    FROM ACCITE ACCITE
                                    WHERE ACCITE.AITGLTRECNO > 0
                                      AND ACCITE.AITPSTRECNO <> 45
                                    GROUP BY ACCITE.AITITERECNO) AUTHTOGL,
                                  ( SELECT PORECOVITE.PORECOVAITITERECNO, SUM(CASE WHEN PORECOVITE.PORECOVAITDRCR = 'C' THEN -1 * PORECOVITE.PORECOVAITBASEAMT ELSE PORECOVITE.PORECOVAITBASEAMT END) AS AUTHTOGLAMT
                                    FROM PORECOVITE PORECOVITE
                                    WHERE PORECOVITE.PORECOVPSTRECNO = 34
                                      AND PORECOVITE.PORECOVGLTRECNO IS NOT NULL
                                    GROUP BY PORECOVITE.PORECOVAITITERECNO) RECOVTOGL
                              WHERE EXPCHA.EXCCHAREC = ITECHG.EXCRECNO
                                AND SALOFFNO.SALOFFNO = LOTPROFITSALOFF.SALOFFNO
                                AND ITECHG.DPRRECNO = SALES_VIEW.DPRRECNO
                                AND SALES_VIEW.LITITENO = LOTPROFITSALOFF.LITITENO
                                AND ITECHG.ICHRECNO = AUTHTOGL.AITITERECNO(+)
                                AND ITECHG.ICHRECNO = RECOVTOGL.PORECOVAITITERECNO(+)
                                AND NVL(EXPCHA.EXCRECOVFROMPL, CONST.C_FALSE) = CONST.C_FALSE
                              GROUP BY SALOFFNO.LOTPROFITMETH), CONST.C_TRUE)
    WHERE LOTPROFITSALOFF.LITITENO = LITITENO_IN;
    
    UPDATE LOTPROFITSALOFF
    SET SALESCOSTAUTH = NVL(( SELECT MIN(CASE
                                          WHEN SALOFFNO.LOTPROFITMETH IN(1, 2) AND ABS(NVL(ITECHG.ICHAPPAMT, 0.0) - NVL(AUTHTOGL.AUTHTOGLAMT, NVL(RECOVTOGL.AUTHTOGLAMT, 0.0))) > 0.009 THEN CONST.C_FALSE
                                          WHEN SALOFFNO.LOTPROFITMETH IN(3, 4) AND ABS(NVL(AUTHTOGL.AUTHTOGLAMT, NVL(RECOVTOGL.AUTHTOGLAMT, 0.0))) < 0.01 THEN CONST.C_FALSE
                                          ELSE CONST.C_TRUE
                                        END)
                              FROM ITECHG ITECHG, EXPCHA EXPCHA, SALOFFNO SALOFFNO, FT_V_DPRSTOLOTS SALES_VIEW,
                                  ( SELECT ACCITE.AITITERECNO, SUM(CASE WHEN ACCITE.AITDRCR = 'C' THEN -1 * ACCITE.AITBASEAMOUNT ELSE ACCITE.AITBASEAMOUNT END) AS AUTHTOGLAMT
                                    FROM ACCITE ACCITE
                                    WHERE ACCITE.AITGLTRECNO > 0
                                    GROUP BY ACCITE.AITITERECNO) AUTHTOGL,
                                  ( SELECT PORECOVITE.PORECOVAITITERECNO, SUM(CASE WHEN PORECOVITE.PORECOVAITDRCR = 'C' THEN -1 * PORECOVITE.PORECOVAITBASEAMT ELSE PORECOVITE.PORECOVAITBASEAMT END) AS AUTHTOGLAMT                               
                                    FROM PORECOVITE PORECOVITE
                                    WHERE PORECOVITE.PORECOVPSTRECNO = 34
                                      AND PORECOVITE.PORECOVGLTRECNO IS NOT NULL
                                    GROUP BY PORECOVITE.PORECOVAITITERECNO) RECOVTOGL
                              WHERE EXPCHA.EXCCHAREC = ITECHG.EXCRECNO
                                AND SALOFFNO.SALOFFNO = LOTPROFITSALOFF.SALOFFNO
                                AND ITECHG.DELRECNO = SALES_VIEW.DELRECNO
                                AND ITECHG.ICHRECNO = AUTHTOGL.AITITERECNO(+)
                                AND SALES_VIEW.LITITENO = LOTPROFITSALOFF.LITITENO
                                AND NVL(EXPCHA.EXCRECOVFROMPL, CONST.C_FALSE) = CONST.C_FALSE
                              GROUP BY SALOFFNO.LOTPROFITMETH), CONST.C_TRUE)
    WHERE LOTPROFITSALOFF.LITITENO = LITITENO_IN
      AND SALESCOSTAUTH = CONST.C_TRUE;
    

    UPDATE LOTPROFITSALOFF
    SET OVERSOLD = NVL((SELECT MAX(CASE WHEN LOTITE_LINK.ORGLITITENO > 0 THEN CONST.C_TRUE ELSE CONST.C_FALSE END)
                        FROM LOTITE_LINK LOTITE_LINK
                        WHERE LOTITE_LINK.ORGLITITENO = LOTPROFITSALOFF.LITITENO
                          AND LOTITE_LINK.TYPEFLAG = 1
                          AND LOTITE_LINK.STATUS = 1), 0)
    WHERE LOTPROFITSALOFF.LITITENO = LITITENO_IN;

    UPDATE LOTPROFITSALOFF
    SET REOPENED = CONST.C_TRUE
    WHERE LOTPROFITSALOFF.LITITENO = LITITENO_IN
      AND PROFITISED = CONST.C_TRUE
      AND REOPENED = CONST.C_FALSE
      AND CASE
            WHEN ABS(PROFIT - PREVPROFIT) > 0.009 THEN CONST.C_TRUE
            WHEN FULLYSOLDINVOICED = CONST.C_FALSE THEN CONST.C_TRUE
            WHEN HASGOODSCOST = CONST.C_FALSE THEN CONST.C_TRUE
            WHEN POCOSTAUTH = CONST.C_FALSE THEN CONST.C_TRUE
            WHEN SALESCOSTAUTH = CONST.C_FALSE THEN CONST.C_TRUE
            ELSE CONST.C_FALSE
          END = CONST.C_TRUE;

    UPDATE LOTPROFIT
    SET REOPENED = CONST.C_TRUE
    WHERE LOTPROFIT.LITITENO = LITITENO_IN
      AND EXISTS(SELECT * FROM LOTPROFITSALOFF WHERE LOTPROFITSALOFF.LITITENO = LOTPROFIT.LITITENO AND LOTPROFITSALOFF.REOPENED = CONST.C_TRUE);

    UPDATE LOTITE
    SET ISEXCEPTION = CONST.C_FALSE
    WHERE LOTITE.LITITENO = LITITENO_IN
     AND EXISTS(SELECT * FROM LOTPROFITSALOFF WHERE LOTITE.LITITENO = LOTPROFITSALOFF.LITITENO AND LOTPROFITSALOFF.REOPENED = CONST.C_TRUE)
     AND ISEXCEPTION = CONST.C_TRUE;

    UPDATE LOTPROFITSALOFF
    SET CANPROFITISE =  CASE
                          WHEN PROFITISED = CONST.C_TRUE AND REOPENED = CONST.C_FALSE THEN CONST.C_FALSE
                          WHEN FULLYSOLDINVOICED = CONST.C_FALSE THEN CONST.C_FALSE
                          WHEN HASGOODSCOST = CONST.C_FALSE THEN CONST.C_FALSE
                          WHEN POCOSTAUTH = CONST.C_FALSE THEN CONST.C_FALSE
                          WHEN SALESCOSTAUTH = CONST.C_FALSE THEN CONST.C_FALSE
                          WHEN OVERSOLD = CONST.C_TRUE THEN CONST.C_FALSE
                          ELSE CONST.C_TRUE
                        END
    WHERE LOTPROFITSALOFF.LITITENO = LITITENO_IN;
  
    UPDATE LOTPROFITSALOFF
    SET LHEPROFITPERC = ( SELECT  CASE
                                    WHEN ABS(SUM(NVL(SUBLOTPROFITSALOFF.NETTSALES, 0.0))) < 0.01 THEN 0.0
                                    ELSE ROUND(SUM(SUBLOTPROFITSALOFF.PROFIT) / ABS(SUM(NVL(SUBLOTPROFITSALOFF.NETTSALES, 0.0))) * 100.0, 2)
                                  END
                          FROM LOTITE LOTITE, LOTPROFITSALOFF SUBLOTPROFITSALOFF
                          WHERE SUBLOTPROFITSALOFF.LHERECNO = LOTPROFITSALOFF.LHERECNO
                            AND NVL(LOTITE.ONRESERVE, CONST.C_FALSE) = CONST.C_FALSE
                            AND LOTITE.LITITENO = SUBLOTPROFITSALOFF.LITITENO(+)
                       )
    WHERE LOTPROFITSALOFF.LHERECNO = ( SELECT DISTINCT SUBLOTPROFITSALOFF.LHERECNO 
                                        FROM LOTPROFITSALOFF SUBLOTPROFITSALOFF
                                        WHERE SUBLOTPROFITSALOFF.LITITENO = LITITENO_IN
                                        );
    
    COMMIT;
  EXCEPTION
    WHEN OTHERS THEN
      ROLLBACK;
      FT_PK_ERRORS.LOG_AND_STOP;
  END LOTPROFITABILITY;

  PROCEDURE LOTPROFITABILITY
  IS
  BEGIN
    FOR LITREC IN (SELECT AUTOCOSTS_PROCESS.LITITENO FROM AUTOCOSTS_PROCESS WHERE AUTOCOSTS_PROCESS.LOTPROFITABILITY = CONST.C_TRUE) LOOP
      LOTPROFITABILITY(LITREC.LITITENO);
    END LOOP;
  EXCEPTION
    WHEN OTHERS THEN
      FT_PK_ERRORS.LOG_AND_STOP;
  END LOTPROFITABILITY;

END FT_PK_PROFITISELOTS ;
/
